#!groovy

@Library("Infrastructure")

def type = "java"

def product = "fpl"
def component = "case-service"

def serviceSecrets = [
  'fpl-${env}': [
    secret('smoke-test-la-username', 'SMOKE_TEST_LA_USER_USERNAME'),
    secret('smoke-test-la-password', 'SMOKE_TEST_LA_USER_PASSWORD')
  ]
]

def otherSecrets = [
  // below is just for CCD in AKS - to be cleaned up when CCD Helm chart is updated to use flex volume for secrets
  's2s-${env}': [
    secret('microservicekey-ccd-gw', 'CCD_API_GATEWAY_S2S_SECRET'),
    secret('microservicekey-ccd-data', 'CCD_DATA_STORE_S2S_SECRET'),
    secret('microservicekey-ccd-definition', 'CCD_DEFINITION_STORE_S2S_SECRET')
  ],
  'ccd-${env}': [
    secret('ccd-api-gateway-oauth2-client-secret', 'CCD_API_GATEWAY_IDAM_CLIENT_SECRET'),
    secret('ccd-as-a-pr-importer-username', 'CCD_CONFIGURER_IMPORTER_USERNAME'),
    secret('ccd-as-a-pr-importer-password', 'CCD_CONFIGURER_IMPORTER_PASSWORD'),
    secret('postcode-info-address-lookup-token', 'ADDRESS_LOOKUP_TOKEN')
  ]
]

static Map<String, Object> secret(String secretName, String envVariable) {
  [ $class: 'AzureKeyVaultSecret',
    secretType: 'Secret',
    name: secretName,
    envVariable: envVariable
  ]
}

def registerSmokeTestCallbacks(String env, String url) {
  registerSmokeTestCallbacks(env, url, null)
}

def registerSmokeTestCallbacks(String env, String url, Closure preparationBlock) {
  steps.before("smoketest:${env}") {
    if (preparationBlock != null) {
      preparationBlock()
    }

    steps.env.URL = url
  }

  steps.after("smoketest:${env}") {
    steps.archiveArtifacts allowEmptyArchive: true, artifacts: 'output/**/*'
  }
}

def archiveCoreCaseDataDefinitions(String env) {
  sh "./bin/build-release-ccd-definition.sh ${env}"
  steps.archiveArtifacts artifacts: "build/ccd-release-config/ccd-fpl-${env}.xlsx"
}

def registerCallbacksForPREVIEW() {
  registerSmokeTestCallbacks('preview', "https://case-management-web-fpl-case-service-pr-${CHANGE_ID}.service.core-compute-preview.internal") {
    sh """
      eval \$(./bin/variables/load-preview-environment-variables.sh ${CHANGE_ID})
      
      ./bin/configurer/add-roles.sh
      ./bin/configurer/import-ccd-definition.sh
    """
  }

  before('functionalTest:preview') {
    env.SKIP_FUNCTIONAL_TESTS = 'false'
  }

  after('functionalTest:preview') {
    steps.archiveArtifacts allowEmptyArchive: true, artifacts: 'output/**/*'
  }
}

def registerCallbacksForSAAT() {
  registerSmokeTestCallbacks('saat', 'https://ccd-case-management-web-saat.service.core-compute-saat.internal')

  after('functionalTest:saat') {
    archiveCoreCaseDataDefinitions('saat')
  }
}

def registerCallbacksForAAT() {
  registerSmokeTestCallbacks('aat', 'https://ccd-case-management-web-aat.service.core-compute-aat.internal')

  after('functionalTest:aat') {
    archiveCoreCaseDataDefinitions('aat')
    archiveCoreCaseDataDefinitions('prod')
  }
}

def registerCallbacksForDEMO() {
  registerSmokeTestCallbacks('demo', 'https://ccd-case-management-web-demo.service.core-compute-demo.internal')

  after('functionalTest:demo') {
    archiveCoreCaseDataDefinitions('demo')
  }
}

withPipeline(type, product, component) {
  onMaster {
    loadVaultSecrets(serviceSecrets)
  }
  onPR {
    loadVaultSecrets(serviceSecrets + otherSecrets)
  }
  disableLegacyDeployment()
  enableDockerBuild()
  enableAksStagingDeployment()
  installCharts()
  enableSlackNotifications('#fpla-tech')

  env.SKIP_FUNCTIONAL_TESTS = 'true'
  env.PROXY_SERVER = "proxyout.reform.hmcts.net:8080"

  after('checkout') {
    onMaster {
      withCredentials([usernamePassword(credentialsId: 'jenkins-github-hmcts-api-token', usernameVariable: 'USERNAME', passwordVariable: 'BEARER_TOKEN')]) {
        try {
          sh '''
            set -e

            git remote set-url origin $(git config remote.origin.url | sed "s/github.com/${BEARER_TOKEN}@github.com/g")

            git fetch origin demo:demo
            git push --force origin HEAD:demo
          '''
        } catch (err) {
          notifyBuildEvent channel: '#fpla-tech', color: 'warning', message: 'Failed to update demo branch'
        }
      }
    }
  }

  registerCallbacksForPREVIEW()
  registerCallbacksForSAAT()
  registerCallbacksForAAT()
  registerCallbacksForDEMO()
}
