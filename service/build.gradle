plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.8.RELEASE'
  id 'org.springframework.boot' version '2.1.7.RELEASE'
  id 'com.github.ben-manes.versions' version '0.22.0'
  id 'org.sonarqube' version '2.7.1'
}

apply plugin: 'info.solidsoft.pitest'
apply plugin: 'org.owasp.dependencycheck'

group = 'uk.gov.hmcts.reform'
version = '1.0.0'

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  // set your environment variables here
  // environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}
check.dependsOn integration

task installDependencies(type: Exec, description: 'Installs Yarn dependencies.') {
  commandLine '/usr/bin/yarn', '--mutex', 'network', '--frozen-lockfile', '--silent', 'install'
}

task awaitApplicationReadiness(type: Exec, description: 'Awaits until application is ready.') {
  commandLine '../bin/configurer/utils/wait-for.sh', System.env.URL
}

task runSmokeTests(type: Exec, description: 'Runs smoke tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'test:smoke'
}

task runFunctionalTest(type: Exec, description: 'Runs E2E tests.') {
  onlyIf(new Spec<Task>() {
    boolean isSatisfiedBy(Task task) {
      String skipTestsEnvironmentVariable = System.env.SKIP_FUNCTIONAL_TESTS
      return skipTestsEnvironmentVariable == null || skipTestsEnvironmentVariable == 'false'
    }
  })
  commandLine '/usr/bin/yarn', '--silent', 'test:functional'
}

task smoke(description: 'Runs the smoke tests.') {
  dependsOn(awaitApplicationReadiness, installDependencies, runSmokeTests)
}

task functional(description: 'Runs the functional tests.') {
  dependsOn(awaitApplicationReadiness, installDependencies, runFunctionalTest)
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.20'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
  toolVersion = "6.14.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("../config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

sonarqube {
  properties {
    property "sonar.projectName", "FPL :: Service"
    property "sonar.jacoco.reportPath", file("${project.buildDir}/jacoco/test.exec")
    property "sonar.jacoco.itReportPath", file("${project.buildDir}/jacoco/integration.exec")
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.coverage.exclusions", "**/model/**"
  }
}

repositories {
  jcenter()
  maven {
    url "https://dl.bintray.com/hmcts/hmcts-maven"
  }
  maven {
    url  "http://repo.spring.io/milestone"
  }
}

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.
def versions = [
  reformLogging: '5.0.1',
  springBoot: springBoot.class.package.implementationVersion,
  springfoxSwagger: '2.9.2'
]

dependencies {
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
  compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot

  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.1.2.RELEASE'

//   The below fixes https://nvd.nist.gov/vuln/detail/CVE-2019-12384 while waiting for spring to pull new version
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9.3'

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: '0.0.4'
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: versions.reformLogging

  compile group: 'uk.gov.hmcts.reform', name: 'pdf-generator', version: '1.0.2'
  compile group: 'uk.gov.hmcts.reform', name: 'idam-client', version: '1.1.1'
  compile group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '5.0.0'
  compile group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: '4.6.4'
  compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.0.0'

  compile group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '3.14.2-RELEASE'

  compile group: 'com.google.guava', name: 'guava', version: '28.0-jre'
  compile group: 'commons-io', name: 'commons-io', version: '2.6'

  compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'

  testCompile (group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot) {
    exclude group: 'junit', module: 'junit'
  }
  testCompile group: 'org.junit.jupiter', name :'junit-jupiter', version: '5.4.2'
  testCompile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.16'

  integrationTestCompile configurations.testRuntime {
    exclude group: 'org.springframework.cloud', module: 'spring-cloud-starter-netflix-hystrix'
    exclude group: 'io.springfox'
  }

  functionalTestCompile configurations.runtime

  smokeTestCompile configurations.runtime
}

mainClassName = 'uk.gov.hmcts.reform.fpl.Application'

bootJar {
  archiveName = 'service.jar'

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

test {
  useJUnitPlatform()
}

integration {
  useJUnitPlatform()
}

pitest {
  pitestVersion = '1.4.8'
  testPlugin = 'junit5'
  targetClasses = ['uk.gov.hmcts.reform.fpl.*']
  testSourceSets = [sourceSets.test, sourceSets.integrationTest]
  threads = 10
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/history'
  historyOutputLocation = 'build/reports/pitest/history'
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 80
  timeoutConstInMillis = 30000
}
